/*******************************************************************************
    Copyright 2007 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************/

/*
 * Definition of Fortress parameters.
 */
module Parameter(Header, Type, Expr, Identifier, Keyword, Symbol, Spacing);

import Header;
import Type;
import Expr;
import Identifier;
import Keyword;
import Symbol;
import Spacing;

/* ValParam ::= Id | ( (w Params)? w ) */
List<Param> ValParam =
     a1:Id
     { yyValue = FortressUtil.<Param, NormalParam>mkList(NodeFactory.makeParam(a1)); }
   / openparen a1:(w Params)? w closeparen
     { if (a1 == null) yyValue = FortressUtil.emptyParams();
       else            yyValue = a1;
     };

/* Params ::=
     (Param w , w)* (Varargs w , w)? Keyword (w , w Keyword)*
   | (Param w , w)*  Varargs
   |  Param (w , w Param)*
 */
List<Param> Params =
     a1s:(Param w comma w)* a2:(Varargs w comma w)? a3:Keyword
       a4s:(w comma w Keyword)*
     { yyValue = FortressUtil.<Param, NormalParam>mkList(a1s.list());
       if (a2 != null) yyValue.add(a2);
       yyValue.add(a3);
       yyValue.addAll(a4s.list());
     }
   / a1s:(Param w comma w)* a2:Varargs
     { yyValue = FortressUtil.<Param, NormalParam>mkList(a1s.list(), a2); }
   /  a1:Param a2s:(w comma w Param)*
     { yyValue = FortressUtil.<Param, NormalParam>mkList(a1, a2s.list()); };

/* Varargs ::= Id w : w Type w ... */
VarargsParam VarargsParam = a1:Id w colon w a2:Type w ellipses
     { VarargsType ty = new VarargsType(a2.getSpan(), a2);
       yyValue = NodeFactory.makeVarargsParam(NodeFactory.makeIdName(a1), ty);
     };
VarargsParam Varargs = VarargsParam ;

/* Keyword ::= Param w = w Expr */
Param Keyword = a1:Param w equals w a2:Expr
     { yyValue = NodeFactory.makeParam(a1, a2); };

/* Param ::= Id (w IsType)? | Type */
NormalParam PlainParam =
     a1:Id a2:(w IsType)?
     { if (a2 == null) yyValue = NodeFactory.makeParam(a1);
       else            yyValue = NodeFactory.makeParam(a1, a2);
     }
   / a1:Type
     { yyValue = NodeFactory.makeParam(new Id(createSpan(yyStart,yyCount), "_"),
                                       a1); };
NormalParam Param = PlainParam ;

/* OpHeaderFront ::=
     opr (w StaticParams)? (w BIG)? w (LeftEncloser | Encloser) w Params w
     (RightEncloser | Encloser) (w := w ( w SubscriptAssignParam w ))?
   | opr (w StaticParams)? w ValParam (w BIG)? w Op
   | opr (w BIG)? w (Op | ExponentOp | Encloser) (w StaticParams)? w ValParam
 */
FnHeaderFront OpHeaderFront =
     opr opa1:(w StaticParams)? big:(w BIG)? w opa2:(LeftEncloser / Encloser) w
     opa3:Params w opa4:(RightEncloser / Encloser) opa5:(w void:colonequals w
     openparen w SubscriptAssignParam w closeparen)?
     { FnName name;
       Span span = createSpan(yyStart,yyCount);
       String left  = opa2.getText();
       String right = opa4.getText();
       if (left.equals("[") && right.equals("]")) {
           if (opa5 == null) name = new SubscriptOp(span);
           else              name = new SubscriptAssign(span);
       } else if (PrecedenceMap.ONLY.matchedBrackets(left, right)) {
           if (big != null) {
               opa2 = NodeFactory.makeOp(opa2.getSpan(), "BIG " + left);
               opa4 = NodeFactory.makeOp(opa4.getSpan(), "BIG " + right);
           }
           name = new Enclosing(span, opa2, opa4);
       } else
           throw new ProgramError(opa2, "Mismatched enclosing operator definition: "
                                        + left + " and " + right);
       if (opa1 == null) opa1 = FortressUtil.emptyStaticParams();
       Option<Param> opparam = Option.wrap(opa5);
       yyValue = new FnHeaderFront(name, opa1, opa3, opparam);
     }
   / opr a1:(w StaticParams)? w a2:ValParam big:(w BIG)? w a3:Op
     { Span span = createSpan(yyStart,yyCount);
       if (big != null)
           a3 = NodeFactory.makeOp(a3.getSpan(), "BIG " + a3.getText());
       if (a1 == null) yyValue = new FnHeaderFront(new PostFix(span,a3), a2);
       else            yyValue = new FnHeaderFront(new PostFix(span,a3), a1, a2);
     }
   / opr big:(w BIG)? w a1:(Op / ExponentOp / Encloser) a2:(w StaticParams)? w
     a3:ValParam
     { Span span = createSpan(yyStart,yyCount);
       if (big != null)
           a1 = NodeFactory.makeOp(a1.getSpan(), "BIG " + a1.getText());
       if (a2 == null) yyValue = new FnHeaderFront(new Opr(span,a1), a3);
       else            yyValue = new FnHeaderFront(new Opr(span,a1), a2, a3);
     };

/* SubscriptAssignParam ::= Varargs | Param */
private Param SubscriptAssignParam =
     Varargs
   / Param ;
