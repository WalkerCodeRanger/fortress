/*******************************************************************************
    Copyright 2007 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************/

/*
 * Definition of Fortress local declarations.
 */
module LocalDecl(Variable, Function, Param, Header, Type, NoNewlineExpr,
                 DelimitedExpr, Identifier, Keyword, Symbol, Spacing);
modify Variable;
import Function;
import Param;
import Header;
import Type;
import NoNewlineExpr;
import DelimitedExpr;
import Identifier;
import Keyword;
import Symbol;
import Spacing;

/* BlockElems ::=
     BlockElem br BlockElems
   | BlockElem (w ;)?
 */
List<Expr> BlockElems =
     a1:BlockElem br a2:BlockElems
     { yyValue = FortressUtil.mkList(a1, a2); }
   / a1:BlockElem
     &(w Elifs / w Else / br CaseClause / br TypecaseTypeRefs / br CaseElse
     / w end / w Catch / w forbid / w finally)
     { yyValue = FortressUtil.mkList(a1); }
   / a1:BlockElem w semicolon
     &(w Elifs / w Else / w CaseClause / w TypecaseTypeRefs / w CaseElse
     / w end / w Catch / w forbid / w finally)
     { yyValue = FortressUtil.mkList(a1); };

/* BlockElem ::=
     LocalVarFnDecl
   | NoNewlineExpr (s , w NoNewlineGeneratorList)?
 */
private Expr BlockElem =
     LocalVarFnDecl
   / a1:NoNewlineExpr a2:(s comma w NoNewlineGeneratorList)?
     { if (a2 == null) yyValue = a1;
       else yyValue = new GeneratedExpr(createSpan(yyStart,yyCount), a1, a2,
                                        FortressUtil.emptyExprs());
     };

/* LocalVarFnDecl ::= LocalFnDecl (br LocalFnDecl)* | LocalVarDecl */
private LetExpr LocalVarFnDecl =
     a1:LocalFnDecl a2s:(br LocalFnDecl)*
     { List<FnDecl> fns = FortressUtil.mkList(a1, a2s.list());
       yyValue = new LetFn(createSpan(yyStart,yyCount), fns,
                           FortressUtil.emptyExprs());
     }
   / LocalVarDecl ;

/* LocalFnDecl ::=
     LocalFnMods? FnHeaderFront FnHeaderClause w = w NoNewlineExpr
 */
private FnDecl LocalFnDecl =
     a1:LocalFnMods? a2:FnHeaderFront a3:FnHeaderClause w equals w
     a4:NoNewlineExpr
     { if (a1 == null) a1 = FortressUtil.emptyModifiers();
       yyValue = FortressUtil.mkFnDecl(createSpan(yyStart,yyCount),a1,a2,a3,a4);
     };

/* LocalVarDecl ::=
     (var w)? LocalVarWTypes s InitVal
   | (var w)? LocalVarWTypes
   | (var w)? LocalVarWoTypes s = s NoNewlineExpr
   | (var w)? LocalVarWoTypes s : s TypeRef s ... (s InitVal)?
   | (var w)? LocalVarWoTypes s : s SimpleTupleType (s InitVal)?
   LocalVarWTypes ::=
     LocalVarWType
   | ( w LocalVarWType (w , w LocalVarWType)+ w )
   LocalVarWType ::= Id s : s TypeRef
   LocalVarWoTypes ::=
     LocalVarWoType
   | ( w LocalVarWoType (w , w LocalVarWoType)+ w )
   LocalVarWoType ::=
     Id
   | Unpasting
 */
DefOrDecl VarDecl :=
     a1:(var w)? a2:VarWTypes s a3:InitVal
     { if (a1 != null) FortressUtil.setMutable(a2, createSpan(yyStart,3));
       else if (a3.getB()) FortressUtil.setMutable(a2);
       yyValue = FortressUtil.mkLocalVarDecl(createSpan(yyStart,yyCount), a2,
                                             a3.getA());
     }
   / a1:(var w)? a2:VarWTypes
     { if (a1 != null) FortressUtil.setMutable(a2, createSpan(yyStart,3));
       yyValue = FortressUtil.mkLocalVarDecl(createSpan(yyStart,yyCount), a2);
     }
   / a1:(var w)? a2:VarWoTypes s equals s a3:NoNewlineExpr
     { if (a1 != null) FortressUtil.setMutable(a2, createSpan(yyStart,3));
       yyValue = FortressUtil.mkLocalVarDecl(createSpan(yyStart,yyCount),a2,a3);
     }
   / a1:(var w)? a2:VarWoTypes s colon s a3:TypeRef s ellipses a4:(s InitVal)?
     { Option<Expr> init;
       if (a1 != null) {
           FortressUtil.setMutableAndType(a2, createSpan(yyStart,3), a3);
           if (a4 == null) init = None.<Expr>make();
           else            init = Some.<Expr>make(a4.getA());
       } else {
           if (a4 == null) {
               init = None.<Expr>make();
               FortressUtil.setType(a2, a3);
           } else {
               init = Some.<Expr>make(a4.getA());
               if (a4.getB()) FortressUtil.setMutableAndType(a2, a3);
               else FortressUtil.setType(a2, a3);
           }
       }
       yyValue = FortressUtil.mkLocalVarDecl(createSpan(yyStart,yyCount), a2,
                                             init);
     }
   / a1:(var w)? a2:VarWoTypes s colon s a3:SimpleTupleType a4:(s InitVal)?
     { Span span = createSpan(yyStart,yyCount);
       List<TypeRef> tys = a3.getElements();
       Option<Expr> init;
       if (a2.size() != tys.size())
           throw new ProgramError(new Id(span, "_"),
                                  "Mismatched numbers of variables and types.");
       if (a1 != null) {
           FortressUtil.setMutableAndType(a2, createSpan(yyStart,3), tys);
           if (a4 == null) init = None.<Expr>make();
           else            init = Some.<Expr>make(a4.getA());
       } else {
           if (a4 == null) {
               init = None.<Expr>make();
               FortressUtil.setType(a2, tys);
           } else {
               init = Some.<Expr>make(a4.getA());
               if (a4.getB()) FortressUtil.setMutableAndType(a2, tys);
               else FortressUtil.setType(a2, tys);
           }
       }
       yyValue = FortressUtil.mkLocalVarDecl(span, a2, init);
     };
LocalVarDecl LocalVarDecl = a1:VarDecl
     { yyValue = (LocalVarDecl)a1; };

LValue VarWType := a1:Id s colon s a2:TypeRef
     { yyValue = FortressUtil.mkLValueBind(createSpan(yyStart,yyCount),a1,a2); };

/* VarWoTypes ::=
     VarWoType
   | ( w VarWoType (w , w VarWoType)+ w )
 */
List<LValue> VarWoTypes =
     a1:VarWoType
     { yyValue = FortressUtil.mkList(a1); }
   / openparen w a1:VarWoType a2s:(w comma w VarWoType)+ w closeparen
     { yyValue = FortressUtil.mkList(a1, a2s.list()); };

LValue VarWoType =
     a1:Id
     { yyValue = FortressUtil.mkLValueBind(createSpan(yyStart,yyCount), a1); }
   / Unpasting ;

/* Unpasting ::= [ w UnpastingElems w ] */
private Unpasting Unpasting = opensquare w UnpastingElems w closesquare ;

/* UnpastingElems ::=
     UnpastingElem
   | UnpastingElem RectSeparator UnpastingElems
 */
private Unpasting UnpastingElems =
     a1:UnpastingElem a2:RectSeparator a3:UnpastingElems
     { yyValue = FortressUtil.unpastingCons(createSpan(yyStart,yyCount),a1,a2,a3); }
   / UnpastingElem ;

/* UnpastingElem ::=
     Id (w [ w UnpastingDim w ])?
   | Unpasting
 */
private Unpasting UnpastingElem =
     a1:Id a2:(opensquare w UnpastingDim w closesquare)?
     { if (a2 == null)
           yyValue = new UnpastingBind(createSpan(yyStart,yyCount), a1,
                                       None.<List<ExtentRange>>make());
       else
           yyValue = new UnpastingBind(createSpan(yyStart,yyCount), a1,
                                       Some.<List<ExtentRange>>make(a2));
     }
   / Unpasting ;

/* UnpastingDim ::= ExtentRange (w BY w ExtentRange)+  */
private List<ExtentRange> UnpastingDim =
     a1:ExtentRange a2s:(w cross w ExtentRange)+
     { yyValue = FortressUtil.mkList(a1, a2s.list()); };

/* CaseClause ::= NoNewlineExpr w => w BlockElems */
CaseClause CaseClause = a1:NoNewlineExpr w match w a2:BlockElems
     { yyValue = new CaseClause(createSpan(yyStart,yyCount), a1, a2); };
