/*******************************************************************************
    Copyright 2007 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************/

/*
 * Definition of Fortress variable declarations.
 */
module Variable(Header, Type, NoNewlineExpr, Identifier, Keyword, Symbol,
                Spacing);

import Header;
import Type;
import NoNewlineExpr;
import Identifier;
import Keyword;
import Symbol;
import Spacing;

/* VarDecl ::=
     VarMods? VarWTypes w InitVal
   | VarMods? IdOrIdTuple w = w NoNewlineExpr
   | VarMods? IdOrIdTuple w : w TypeRef w ... w InitVal
   | VarMods? IdOrIdTuple w : w SimpleTupleType w InitVal
 */
DefOrDecl VarDecl =
     a1:VarMods? a2:VarWTypes w a3:InitVal
     { if (a1 != null) {
           if (a3.getB()) a2 = FortressUtil.setModsAndMutable(a2, a1);
           else a2 = FortressUtil.setMods(a2, a1);
       } else {
           if (a3.getB()) a2 = FortressUtil.setMutable(a2);
       }
       yyValue = new VarDecl(createSpan(yyStart,yyCount), a2, a3.getA());
     }
   / a1:VarMods? a2:IdOrIdTuple w equals w a3:NoNewlineExpr
     { List<LValue> lvs;
       if (a1 != null) lvs = FortressUtil.ids2Lvs(a2, a1);
       else lvs = FortressUtil.ids2Lvs(a2);
       yyValue = new VarDecl(createSpan(yyStart,yyCount), lvs, a3);
     }
   / a1:VarMods? a2:IdOrIdTuple w colon w a3:TypeRef w ellipses w a4:InitVal
     { List<LValue> lvs;
       if (a1 != null) {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, a1, a3, true);
           else           lvs = FortressUtil.ids2Lvs(a2, a1, a3, false);
       } else {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, a3, true);
           else           lvs = FortressUtil.ids2Lvs(a2, a3, false);
       }
       yyValue = new VarDecl(createSpan(yyStart,yyCount), lvs, a4.getA());
     }
   / a1:VarMods? a2:IdOrIdTuple w colon w a3:SimpleTupleType w a4:InitVal
     { Span span = createSpan(yyStart,yyCount);
       List<TypeRef> tys = a3.getElements();
       if (a2.size() != tys.size())
           throw new ProgramError(new Id(span, "_"),
                                  "Mismatched numbers of variables and types.");
       List<LValue> lvs;
       if (a1 != null) {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, a1, tys, true);
           else           lvs = FortressUtil.ids2Lvs(a2, a1, tys, false);
       } else {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, tys, true);
           else           lvs = FortressUtil.ids2Lvs(a2, tys, false);
       }
       yyValue = new VarDecl(span, lvs, a4.getA());
     };

/* Each modifier cannot appear more than once. */
/* VarMods ::= (VarMod w)+ */
List<Modifier> VarMods = a1s:(VarMod w)+
     { yyValue = FortressUtil.mkList(a1s.list());
       FortressUtil.noDuplicate(yyValue);
     };

/* VarMod ::= AbsVarMod | private */
private Modifier VarMod =
     AbsVarMod
   / a1:private { yyValue = new Modifier.Private(createSpan(yyStart,yyCount)); };

/* VarWTypes ::=
     VarWType
   | ( w VarWType (w , w VarWType)+ w )
 */
List<LValue> VarWTypes =
     a1:VarWType
     { yyValue = FortressUtil.mkList(a1); }
   / openparen w a1:VarWType a2s:(w comma w VarWType)+ w closeparen
     { yyValue = FortressUtil.mkList(a1, a2s.list()); };

/* VarWType ::= Id w IsType */
LValue VarWType = a1:Id w a2:IsType
     { yyValue = FortressUtil.mkLValueBind(createSpan(yyStart,yyCount),a1,a2); };

/* InitVal ::= (=|:=) w NoNewlineExpr */
com.sun.fortress.interpreter.useful.Pair<Expr,Boolean> InitVal = a1:(equals / colonequals) w a2:NoNewlineExpr
     { yyValue = new com.sun.fortress.interpreter.useful.Pair<Expr,Boolean>(a2, a1.equals(":=")); };

/* AbsVarDecl ::=
     AbsVarMods? VarWTypes
   | AbsVarMods? IdOrIdTuple w : w TypeRef w ...
   | AbsVarMods? IdOrIdTuple w : w SimpleTupleType
 */
AbsVarDecl AbsVarDecl =
     a1:AbsVarMods? a2:VarWTypes
     { if (a1 != null) a2 = FortressUtil.setMods(a2, a1);
       yyValue = new AbsVarDecl(createSpan(yyStart,yyCount), a2);
     }
   / a1:AbsVarMods? a2:IdOrIdTuple w colon w a3:TypeRef w ellipses
     { List<LValue> lvs;
       if (a1 != null) lvs = FortressUtil.ids2Lvs(a2, a1, a3, false);
       else            lvs = FortressUtil.ids2Lvs(a2, a3, false);
       yyValue = new AbsVarDecl(createSpan(yyStart,yyCount), lvs);
     }
   / a1:AbsVarMods? a2:IdOrIdTuple w colon w a3:SimpleTupleType
     { Span span = createSpan(yyStart,yyCount);
       List<TypeRef> tys = a3.getElements();
       if (a2.size() != tys.size())
           throw new ProgramError(new Id(span, "_"),
                                  "Mismatched numbers of variables and types.");
       List<LValue> lvs;
       if (a1 != null) lvs = FortressUtil.ids2Lvs(a2, a1, tys, false);
       else            lvs = FortressUtil.ids2Lvs(a2, tys, false);
       yyValue = new AbsVarDecl(span, lvs);
     };

/* Each modifier cannot appear more than once. */
/* AbsVarMods ::= (AbsVarMod w)+ */
List<Modifier> AbsVarMods = a1s:(AbsVarMod w)+
     { yyValue = FortressUtil.mkList(a1s.list());
       FortressUtil.noDuplicate(yyValue);
     };

/* AbsVarMod ::= var | test */
private Modifier AbsVarMod =
     a1:var  { yyValue = new Modifier.Var(createSpan(yyStart,yyCount)); }
   / a1:test { yyValue = new Modifier.Test(createSpan(yyStart,yyCount)); };
